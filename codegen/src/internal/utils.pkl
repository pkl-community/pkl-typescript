module pkl.typescript.internal.utils

import "pkl:reflect"

local escaper = (c: Char) ->
  if (c == "\n") #"\n"#
  else if (c == "\"") #"\""#
  else if (c == #"\"#) #"\\"#
  else c

/// Turn the Pkl string into a TypeScript string literal.
///
/// Renders raw string literals if the incoming string is multiline, or contains quotes.
///
/// Only newlines, double quotes and backslash literals need to be escaped in TypeScript strings.
function toTypescriptString(str: String): String =
  if ((str.contains("\n") || str.contains("\"")) && !str.contains("`")) "`" + str + "`"
  else "\"" + str.chars.map(escaper).join("") + "\""

/// Converts a Pkl declaration (class, property, typealias) into a TypeScript name.
/// If a member has an explicit `@typescript.Name` annotation, use it.
///
/// Otherwise, normalize the name and return it.
///
/// Normalization rules:
///
/// 1. Any non-letter and non-digit characters get stripped, and each proceding letter gets capitalized.
/// 2. If a name does not start with a latin alphabet character, prefix with `N`.
/// 3. Capitalize names so they get exported.
function toTypescriptName(source: reflect.Declaration): String =
  source
    .annotations
    .findOrNull((it) -> it.getClass().toString() == "pkl.typescript.typescript#Name")
    ?.value
  ??
    // edge case: if the source is the module's companion class, use the module name and not the class name.
    let (_name =
      if (source is reflect.Class && source.enclosingDeclaration.moduleClass == source)
        source.enclosingDeclaration.name.split(".").last
      else source.name
    )
      normalizeName(_name)

function toTypescriptPropertyName(source: reflect.Declaration): String =
  source
    .annotations
    .findOrNull((it) -> it.getClass().toString() == "pkl.typescript.typescript#Name")
    ?.value
  ??
    // edge case: if the source is the module's companion class, use the module name and not the class name.
    let (_name =
      if (source is reflect.Class && source.enclosingDeclaration.moduleClass == source)
        source.enclosingDeclaration.name.split(".").last
      else source.name
    )
      camelCase(_name)

/// Sourced from: https://developer.mozilla.org/en-US/docs/Web/JavaScript/Reference/Lexical_grammar#keywords
keywords: List<String> = List(
  // Reserved words
  "break",
  "case",
  "catch",
  "class",
  "const",
  "continue",
  "debugger",
  "default",
  "delete",
  "do",
  "else",
  "export",
  "extends",
  "false",
  "finally",
  "for",
  "function",
  "if",
  "import",
  "in",
  "instanceof",
  "new",
  "null",
  "return",
  "super",
  "switch",
  "this",
  "throw",
  "true",
  "try",
  "typeof",
  "var",
  "void",
  "while",
  "with",

  // Reserved in strict mode
  "let",
  "static",
  "yield",

  // Reserved in async code
  "await",

  // Future reserved
  "enum",

  // Future reserved in strict mode

  "implements",
  "interface",
  "package",
  "private",
  "protected",
  "public",

  // Future reserved in older EMCAScript standards
  "abstract",
  "boolean",
  "byte",
  "char",
  "double",
  "final",
  "float",
  "goto",
  "int",
  "long",
  "native",
  "short",
  "synchronized",
  "throws",
  "transient",
  "volatile",


  // Identifiers with special meaning
  "arguments",
  "as",
  "async",
  "eval",
  "from",
  "get",
  "of",
  "set"
)

/// As per: https://stackoverflow.com/questions/1661197/what-characters-are-valid-for-javascript-variable-names/9337047#9337047
/// > An identifier must start with $, _, or any character in the Unicode categories “Uppercase letter (Lu)”, “Lowercase letter (Ll)”, “Titlecase letter (Lt)”, “Modifier letter (Lm)”, “Other letter (Lo)”, or “Letter number (Nl)”.
/// > The rest of the string can contain the same characters, plus any U+200C zero width non-joiner characters, U+200D zero width joiner characters, and characters in the Unicode categories “Non-spacing mark (Mn)”, “Spacing combining mark (Mc)”, “Decimal digit number (Nd)”, or “Connector punctuation (Pc)”.
isValidTypescriptName = (it: String) ->
  if (keywords.contains(it))
    throw("""
      Name `\(it)` is not valid because it clashes with a JavaScript/TypeScript keyword`.
      """)
  else it.matches(Regex(#"(?u)\p{L}[\p{L}\d_]+"#))

function renderDocComment(docComment: String, indent: String) =
  docComment
    .split(Regex(#"\r?\n"#))
    .map((it) ->
      if (it.trim().isBlank) "\(indent)//"
      else "\(indent)// \(it)"
    )
    .join("\n")


function canBeNormalizedToEnumName(name: String) =
  name == "" || splitNameOnNonLettersOrDigits(name).length > 0

function normalizeEnumName(name: String) =
  if (name == "") "Empty"
  else normalizeName(name)

function splitNameOnNonLettersOrDigits(name: String) =
  name.split(Regex(#"(?u)[^\p{L}\d]"#))

function normalizeName(name: String) =
  let (parts = splitNameOnNonLettersOrDigits(name))
  // TODO(Jason): Consider camelCase instead of PascalCase. Types vs variables?
  let (pascaled = parts.map((p) -> p.capitalize()).join(""))
    if (pascaled[0].matches(Regex(#"[^A-Z]"#)))
      "N" + pascaled
    else
      pascaled

function camelCase(str: String) =
  let (parts = splitNameOnNonLettersOrDigits(str))
  parts.mapIndexed((i, p) -> if (i == 0) p.decapitalize() else p.capitalize()).join("")

function snakeCase(str: String): String =
  let (parts = splitNameOnNonLettersOrDigits(str))
  let (separator = "_")
  parts.map((part) ->
    part.replaceAllMapped(
      Regex("([a-z0-9])([A-Z])"),
      (match) -> "\(match.groups[1])\(separator)\(match.groups[2])"
    )
  ).join(separator).toLowerCase()

function renderImports(imports: List<String>): String =
  let (distinctImports = imports.distinct)
    new Listing {
      for (_, mod in distinctImports) {
        // Use namespaced imports saves having to detect which values
        // should be named-imported or relying on default export.
        // This is primarily useful for pklTypescript itself.
        "import * as \(camelCase(mod.split("/").last)) from \"\(mod)\""
      }
    }.join("\n")
